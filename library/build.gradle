apply plugin: 'android-library'

android {
    compileSdkVersion 19
    buildToolsVersion '20.0.0'

    defaultConfig {
        applicationId "me.relex.circleindicator"
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:20+'
    compile 'com.nineoldandroids:library:2.4.+'
}

//////////////////////////
apply plugin: 'maven'
apply plugin: 'signing'

version = "1.0.0"
group = "me.relex"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {

        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'CircleIndicator'
            packaging 'aar'
            description 'a lightweight viewpager indicator like in nexus 5 launcher'
            url 'https://github.com/ongakuer/CircleIndicator'

            scm {
                url 'scm:git@github.com:ongakuer/CircleIndicator.git'
                connection 'scm:git@github.com:ongakuer/CircleIndicator.git'
                developerConnection 'scm:git@github.com:ongakuer/CircleIndicator.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'relex'
                    name 'relex'
                    email 'wwwliangxin@gmail.com'
                }
            }
        }
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}
